#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;
	jmp Test

Loop:
	mrmovq  (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9	# read val from src...
	mrmovq 16(%rdi), %r10	# read val from src...
	mrmovq 24(%rdi), %r11	# read val from src...
	
	rmmovq %r8,  (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)	# ...and store it to dst
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	rmmovq %r11, 24(%rsi)	# ...and store it to dst

	andq %r8, %r8
	jle L3
	iaddq $1, %rax
L3:
	andq %r9, %r9
	jle L2
	iaddq $1, %rax
L2:
	andq %r10, %r10
	jle L1
	iaddq $1, %rax
L1:
	andq %r11, %r11
	jle L4
	iaddq $1, %rax
L4:
	iaddq $32, %rdi
	iaddq $32, %rsi
Test:
	iaddq $-4, %rdx	
	jge Loop			# if so, goto Loop:

LessThan4:
	iaddq $3, %rdx
	jl Done

	mrmovq (%rdi), %r10	# read val from src...
	rrmovq %rax, %r11
	iaddq $1, %r11
	rmmovq %r10, (%rsi)	# ...and store it to dst
	andq %r10, %r10		# val <= 0?
	cmovg %r11, %rax
	iaddq $-1, %rdx
	jl Done			# if so, goto Loop:

	mrmovq 8(%rdi), %r10	# read val from src...
	rrmovq %rax, %r11
	iaddq $1, %r11
	rmmovq %r10, 8(%rsi)	# ...and store it to dst
	andq %r10, %r10		# val <= 0?
	cmovg %r11, %rax
	iaddq $-1, %rdx
	jl Done			# if so, goto Loop:

	mrmovq 16(%rdi), %r10	# read val from src...
	rrmovq %rax, %r11
	iaddq $1, %r11
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	andq %r10, %r10		# val <= 0?
	cmovg %r11, %rax

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
